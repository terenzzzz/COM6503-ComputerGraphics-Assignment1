#version 330 core

in vec3 aPos;
in vec3 aNormal;
in vec2 aTexCoord;

out vec4 fragColor;

uniform sampler2D first_texture;
uniform vec3 viewPos;

struct Light {
  vec3 position;
  vec3 ambient;
  vec3 diffuse;
  vec3 specular;
};

uniform Light light1;

struct Material {
  vec3 ambient;
  vec3 diffuse;
  vec3 specular;
  float shininess;
}; 
  
uniform Material material;

void main() {
  // ambient
  vec3 ambient = light1.ambient * texture(first_texture, aTexCoord).rgb;
  
  // diffuse
  vec3 norm = normalize(aNormal);
  vec3 lightDir = normalize(light1.position - aPos);
  float diff = max(dot(norm, lightDir), 0.0);
  vec3 diffuse = light1.diffuse * diff * texture(first_texture, aTexCoord).rgb;
  
  // specular 
  vec3 viewDir = normalize(viewPos - aPos);
  vec3 reflectDir = reflect(-lightDir, norm);  
  float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);
  vec3 specular = light1.specular * spec * material.specular;

  vec3 result = ambient + diffuse + specular;
  fragColor = vec4(result, 1.0);
}